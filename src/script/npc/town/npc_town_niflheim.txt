//========================================================================================
// [神] The Japanda Project [習]
// http://opensvn.csie.org/Japanda
// ---------------------------------------------------------------------------------------
// - 尼芙菲姆 城鎮腳本 -
//========================================================================================
nif_in.gat,37,93,1,	shop	武器商人	801,1301:500,1351:5400,1354:15500,1357:34000,1360:55000
nif_in.gat,37,84,1,	shop	防御具商人	801,2501:1000,2501:1000,2503:5000,2503:5000,2505:32000,2505:32000
nif_in.gat,145,23,1,	shop	道具商人	801,535:15,1062:374,902:12,7106:672,537:1000,7154:160,1052:46,934:600
//==============================================================================
//event_key&1	阿雷戈
//event_key&2	詩人
//event_key&4	庫茲咖希納
//event_key&8	墓石１
//event_key&16	墓石１
//event_key&32	墓石２
//event_key&64	鋼琴安裝上了
//==============================================================================
niflheim.gat,52,147,3	script	柯資	796,{
	mes "[柯資]";
	mes "活著的人不准進來";
	mes "這裡 -! 不允許";
	mes "活著的人通行-!";
	close;
}

niflheim.gat,153,215,4	script	妃依琳	794,{
	mes "[妃依琳]";
	mes "幹嘛要來到這麼危險的村落呢?";
	mes "快點回去吧";
	close;
}

nif_in.gat,16,27,1	script	死雞	800,{
	percentheal -5,0;
	mes "[瞎眼雞]";
	mes "活著的時候被人們吃掉了，";
	mes "現在換我來吃你了，咕咕咕，";
	mes "啊啊，沒想到死了後還會有這種世界，咕咕咕";
	close;
}

nif_in.gat,16,30,4	script	死的 吸血蝙蝠	799,{
	percentheal -5,0;
	mes "[帕豆曼]";
	mes "不管活著或死了，人類的血";
	mes "還是很好喝，";
	mes "什麼? 你也想要";
	mes "被我吸血嗎?";
	close;
}

niflheim.gat,195,211,6	script	其甘提阿	796,3,3,{
//山羊頭盔、獸角頭飾裝備時→LStart2;
	mes "[其甘提阿]";
	mes "死靈騎士總是看的一清二楚";
	close;
LStart2:
	mes "["+strcharinfo(0)+"]";
	mes "我有什麼用？";
	close;
OnTouch:
//山羊頭盔、獸角頭飾無裝備時
	mes "[其甘提阿]";
	mes "等一下！";
	close;
}

niflheim.gat,224,243,3	script	阿雷戈	795,{
	mes "[阿雷戈]";
	mes "我喜歡肉 嗯哈，";
	mes "你看起來...好好吃喔";
	next;
	mes "[阿雷戈]";
	mes "我要吃你";
	next;
	mes "-嘖 嘖 嘖 -";
	if(event_key&1) goto L_2ND;
	percentheal -80,0;
	next;
	mes "[阿雷戈]";
	mes "好久沒吃到這麼好吃的肉了...";
	mes "是西式烤肉師傅等級，喜歡的肉，";
	mes "相似服務腓力牛排定食時，";
	mes "交叉附帶做出來的話，";
	mes "應該會很好吃... ";
	next;
	mes "[阿雷戈]";
	mes "用這個來當謝禮，是不久前";
	mes "在地上撿到的，嗝喔";
	getitem 7184,1;
	set event_key,event_key|1;
	close;
L_2ND:
	percentheal -30,0;
	close;
}
nif_in.gat,105,81,3	script	克雷佑	794,{
	mes "[克雷佑]";
	mes "活在世上的人類啊..";
	mes "不知從何時，從哪個人開始..";
	mes "什麼都不知道的詩，";
	mes "就這樣流傳下來";
	next;
	mes "[克雷佑]";
	mes "^FF0000當日落掛在西山時，^000000";
	mes "^FF0000紫色黃昏所指的地方，^000000";
	mes "^FF0000漂亮的旋律纏繞在身上時，^000000";
	mes "^FF0000是避開死靈騎士的鑰匙^000000";
	next;
	mes "[克雷佑]";
	mes "是什麼意義呢... 再仔細看的話，";
	mes "又非常的深奧，";
	mes "確實像是含有，";
	mes "什麼秘密的詩";
	next;
	if (!(event_key&1) || (event_key&2)) close;
//	if ((event_key&1) && !(event_key&2)) goto LStart2;
//	close;
//LStart2:
	next;
	set @rand,rand(4);
	if (@rand == 1) goto Lrand1;
	if (@rand == 2) goto Lrand2;
	if (@rand == 3) goto Lrand3;
		mes "[克雷佑]";
		mes "那麼在此測驗一下! 請把剛剛所聽到";
		mes "那首詩的第一句，";
		mes "完整的寫一遍!";
		next;
		input @nfword$;
		if (@nfword$ == "當日落掛在西山時") goto LSucsess;
			goto LWrong;
Lrand1:
		mes "[克雷佑]";
		mes "那麼在此測驗一下! 請把剛剛所聽到";
		mes "那首詩的第二句，";
		mes "完整的寫一遍!";
		next;
		input @nfword$;
		if (@nfword$ == "紫色黃昏所指的地方，") goto LSucsess;
			goto LWrong;
Lrand2:
		mes "[克雷佑]";
		mes "那麼在此測驗一下! 請把剛剛所聽到";
		mes "那首詩的第三句，";
		mes "完整的寫一遍!";
		next;
		input @nfword$;
		if (@nfword$ == "漂亮的旋律纏繞在身上時，") goto LSucsess;
			goto LWrong;
Lrand3:
		mes "[克雷佑]";
		mes "那麼在此測驗一下! 請把剛剛所聽到";
		mes "那首詩的第四句，";
		mes "完整的寫一遍!";
		next;
		input @nfword$;
		if (@nfword$ == "是避開死靈騎士的鑰匙") goto LSucsess;
			goto LWrong;
LWrong:
	mes "[克雷佑]";
	mes "唉唷... 如果有專心聽的話，";
	mes "一定會答對的!";
	mes "好好的想想之後，";
	mes "再來挑戰吧!";
	close;
LSucsess:
	mes "[克雷佑]";
	mes "喔喔...有注意聽啊!";
	mes "我克雷佑，身為吟遊詩人，";
	mes "你是最受歡迎的聽眾，";
	mes "這是送你的獎勵";
	next;
	set event_key,event_key|2;
	getitem 7184,1;
	mes "[克雷佑]";
	mes "那麼希望對其他吟遊詩人，";
	mes "也是一樣要有禮貌";
	close;

}

nif_in.gat,31,20,3	script	庫茲咖希納	794,{
	mes "[庫茲咖希納]";
	mes "目前生意真是很差...";
	mes "上輩子我的店也是";
	mes "生意很差呢~!";
	if (!(event_key&1) || (event_key&4)) close;
	next;
	mes "[庫茲咖希納]";
	mes "喂 那位! 出去時幫我，";
	mes "把這個丟掉再走";
	set event_key,event_key|4;
	getitem 7184,1;
	next;
	mes "[庫茲咖希納]";
	mes "到底生意為什麼會";
	mes "這麼差呢... 嘀咕嘀咕嘀咕嘀咕";
	close;
}

niflheim.gat,208,103,0	script	#墓1	111,1,1,{
	close;
OnTouch:
	if (!(event_key&1) || !(event_key&2) || !(event_key&4)) close;
	if ((event_key&8) && (event_key&16)) close;
	if (event_key&8) goto LStart2;
	mes "- 你發現在墳墓附近 -";
	mes "- 有個東西 -";
	mes "- 被埋沒了一半 -";
	mes "- 是什麼的碎片? -";
	mes "- 像是修整過的樣子 -";
	next;
	mes "- 要撿起來嗎? -";
	next;
	menu "是的",L1,"不是",-;
		close;
	L1:
		getitem 7184,1;
		set event_key,event_key|8;
		close;
LStart2:
	mes "- 你發現在墳墓附近 -";
	mes "- 有個東西 -";
	mes "- 被埋沒了一半 -";
	mes "- 是什麼的碎片? -";
	mes "- 像是修整過的樣子 -";
	next;
	mes "- 要撿起來嗎? -";
	next;
	menu "是的",L2,"不是",-;
		close;
	L2:
		getitem 7184,1;
		set event_key,event_key|16;
		close;
}

niflheim.gat,169,71,0	script	#墓2	111,1,1,{
		close;
OnTouch:
	if (event_key&32) close;
	if !((event_key&8) && (event_key&16)) close;
	mes "- 你發現在墳墓附近 -";
	mes "- 有個東西 -";
	mes "- 被埋沒了一半 -";
	mes "- 是什麼的碎片? -";
	mes "- 像是修整過的樣子 -";
	next;
	mes "- 要撿起來嗎? -";
	next;
	menu "是的",L1,"不是",-;
		close;
	L1:
		getitem 7184,1;
		set event_key,event_key|32;
		close;
}

nif_in.gat,156,93,5	script	小孩	793,{
	mes "[阿拉克娜 安]";
	mes "這裡，是哪裡呢...? 睡一覺起來，";
	mes "就來到這裡了... 我媽媽，";
	mes "沒有看到我媽媽嗎? 嗯...? 嗚嗚嗚...";
	mes "我想要回家...";
	next;
	menu "關於魔女？",-,"關於詛咒？",L2,"停止對話",L3;
		mes "[阿拉克娜 安]";
		mes "魔女...? 我不知道所謂的魔女...";
		mes "雖然在童話書裡有讀過，";
		mes "不過不知道真的有沒有那種人，";
		mes "如果有的話一定很可怕啊...";
		next;
		mes "[阿拉克娜 安]";
		mes "可是為什麼會這麼冷呢...?";
		if (sex == 1) mes "哥哥你不會冷嗎? 好奇怪... 呼唷";
		if (sex == 0) mes "姊姊你不會冷嗎? 好奇怪... 呼唷";
		mes "想回到我溫暖的家，";
		mes "幫幫忙吧... 嗯...? 嗯...?";
		close;
	L2:
		mes "[阿拉克娜 安]";
		mes "詛咒... 想起奶奶曾經";
		mes "所說過的話，她說有種方法";
		mes "可以躲避無法以祝福";
		mes "解開的狠毒詛咒";
		next;
		mes "[阿拉克娜 安]";
		mes "克拉圖(Klaatu)...";
		mes "北拉塔(Verata).....";
		mes "再來是... 什麼... 什麼的...? 是什麼啦?";
		mes "最後面記不起來了";
		next;
		mes "[阿拉克娜 安]";
		mes "她還說過按照順序念那3種咒文的話，";
		mes "可以從被詛咒的命運中逃過，";
		mes "現在已不太記得她主要在";
		mes "講什麼了... 我也忘了";
		close;
	L3:
		mes "[阿拉克娜 安]";
		mes "好冷... 好冷喔... 想要回到";
		mes "家裡... 方便的話帶我一起走..";
		mes "這裡太可怕了啦.....";
		mes "拜託你幫幫忙... 救救我...";
		close;
}

niflheim.gat,350,258,1	script	被詛咒的 靈魂	802,{
	killmonster "niflheim.gat","nif_soul";
	mes "[艾書 布路斯]";
	mes "在你身上感受到強烈的詛咒啊啊啊，";
	mes "我拒絕你的存在啊啊啊啊，";
	mes "不退回去否則會死啊啊啊，";
	mes "不准動我的書啊啊啊";
	next;
	menu "拿起第一本書",-,"拿起第二本書",L_MENU_2,"拿起第三本書",L_MENU_3,"乖乖的退回去",L_MENU_4;
		mes "[艾書 布路斯]";
		mes "不要隨便動我的書啊啊!!";
		mes "喀哈啊啊! 詛咒你被咬被撕被抓傷!!";
		mes "永遠被埋在束縛的詛咒裡!!";
		monster "niflheim.gat",349,259,"--ja--",1195,1,"nif_soul";
		close;
	L_MENU_2:
		mes "[艾書 布路斯]";
		mes "我說過不准動我的書啊啊...";
		mes "給我滾遠一點..";
		mes "不要妨礙我休息啊啊啊!";
		close2;
		warp "niflheim.gat",34,162;
		end;
	L_MENU_3:
		mes "[艾書 布路斯]";
		mes "[艾書 布路斯]";
		mes "喀哈哈啊啊.... 相當有膽量";
		mes "的傢伙啊啊...!! 來...";
		mes "開始唸咒文吧啊啊啊...!!";
		set @nif_soul,0;
		next;
			menu "克羅柏(Clover)",L_MENU_3_1,"克拉圖(Klaatu)",-,"克雷易托思(Kleitos)",L_MENU_3_1;
				set @nif_soul,@nif_soul+1;
		L_MENU_3_1:
			menu "北利特(Verit)",L_MENU_3_2,"北利塔司(Veritas)",L_MENU_3_2,"北拉塔 (Verata)",-;
				set @nif_soul,@nif_soul+1;
		L_MENU_3_2:
			menu "內塔易(Necktie)",L_MENU_3_3,"內克利司(Necklace)",L_MENU_3_3,"內羅(Nero)",L_MENU_3_3,"^FFFFFF尼圖(Nictu)^000000",-;
				set @nif_soul,@nif_soul+1;
		L_MENU_3_3:
			if (@nif_soul==3) goto L_MENU_3_OK;
			mes "[艾書 布路斯]";
			mes "哈哈哈啊啊啊!!! 咒文錯了!";
			mes "你即將要面臨死亡了喔喔喔，";
			mes "願你一輩子都受到詛咒吧啊啊啊...!!";
			monster "niflheim.gat",345,259,"--ja--",1152,1,"nif_soul";
			monster "niflheim.gat",347,261,"--ja--",1152,1,"nif_soul";
			monster "niflheim.gat",344,253,"--ja--",1152,1,"nif_soul";
			monster "niflheim.gat",346,251,"--ja--",1152,1,"nif_soul";
			monster "niflheim.gat",349,249,"--ja--",1152,1,"nif_soul";
			monster "niflheim.gat",350,260,"--ja--",1152,1,"nif_soul";
			monster "niflheim.gat",353,256,"--ja--",1152,1,"nif_soul";
			close;
		L_MENU_3_OK:
//他ズ條件やペヌ
			mes "[艾書 布路斯]";
			mes "咒文對了啊啊啊啊啊啊.....";
			mes "不過詛咒沒有解開啊啊啊...";
			mes "喀哈哈啊啊.....!!!";
			close;
	L_MENU_4:
		mes "[艾書 布路斯]";
		mes "喀哈啊啊! 想的不錯啊啊啊啊...";
		mes "我會幫你祈禱活著回去啊啊啊....";
		close;
}

nif_in.gat,115,181,0	script	#鋼琴в	111,1,1,{
	close;
OnTouch:
	if ((event_key&64) || !(event_key&32)) close;
	mes "- 你在鋼琴脫落的部分 -";
	mes "- 把鍵盤一個一個 -";
	mes "- 插進去了，可是 -";
	mes "- 發現最左邊 -";
	mes "- 的部分缺少一個 -";
	mes "- 鍵盤 -";
	set event_key,event_key|64;
	close;
}

nif_in.gat,118,151,1	script	#鋼琴3	111,1,1,{
	close;
OnTouch:
	if !(event_key&64) end;
	mes "- 你身體延長的影子 -";
	mes "- 剛好迭在脫落 -";
	mes "- 的鋼琴鍵盤上 -";
	mes "- 的那一刻 -";
	next;
	soundeffect "complete.wav",0;
	next;
	mes "- 身體有著變輕的感覺， -";
	mes "- 周圍的視野也 -";
	mes "- 開始模糊不清了-";
	close2;
	warp "nif_in.gat",179,163;
	end;
}

nif_in.gat,188,168,3	script	魔女	792,1,1,{
	close;
OnTouch:
	mes "[奇樂凱娜]";
	mes "嗯?... 你不是活的人嗎，";
	mes "來到這裡，";
	mes "應該不容易啊...";
	mes "不管理由是什麼，這裡不是活著的人";
	mes "可以來的地方";
	next;
	mes "[奇樂凱娜]";
	mes "利用我的力量把你傳到";
	mes "外面去，以後最好是";
	mes "不要再來了";
	close2;
	warp "umbala.gat",138,208;
	end;
}

//別Event
prt_in.gat,161,20,4	script	阿爾撒	92,{close;}
umbala.gat,215,181,4	script	比利	733,{close;}
